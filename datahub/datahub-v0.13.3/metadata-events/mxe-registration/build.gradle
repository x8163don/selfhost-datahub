plugins {
  id 'java'
  id 'pegasus'
}

configurations {
  avroOriginal
}

dependencies {
  implementation project(':metadata-events:mxe-avro')
  implementation project(':metadata-models')
  implementation spec.product.pegasus.dataAvro

  testImplementation project(':test-models')
  testImplementation project(path: ':test-models', configuration: 'testDataTemplate')

  avroOriginal project(path: ':metadata-models', configuration: 'avroSchema')

  constraints {
    implementation(externalDependency.log4jCore) {
        because("previous versions are vulnerable to CVE-2021-45105")
    }
    implementation(externalDependency.log4jApi) {
        because("previous versions are vulnerable to CVE-2021-45105")
    }
  }
}

// copy original MXE avro schema from metadata-models to resources
task copyOriginalMXESchemas(type: Copy) {
  dependsOn configurations.avroOriginal

  from { // use of closure defers evaluation until execution time
    configurations.avroOriginal.collect { zipTree(it) }
  }
  into("src/main/resources/")
  include("avro/com/linkedin/mxe/")
}

compileJava.dependsOn copyOriginalMXESchemas
processResources.dependsOn copyOriginalMXESchemas

clean {
  project.delete("src/main/resources/avro")
}